plugins {
    id 'com.google.cloud.tools.appengine' version "${googleAppengineVersion}"
    id 'io.spring.dependency-management'
    id 'java'
    id 'org.springframework.boot'
    id 'jacoco-report-aggregation'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

bootJar {
    mainClass.set('ksch.BackendApplication')
    archiveFileName = 'app.jar'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation project(':ksch.administration')
    implementation project(':ksch.patientmanagement')
    implementation project(':ksch.billing')
    implementation project(':ksch.visit')
}

test {
    useJUnitPlatform()
}

appengine {
    stage {
        artifact = 'build/libs/app.jar'
    }
    deploy {
        version = 'GCLOUD_CONFIG'
        projectId = 'GCLOUD_CONFIG'
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

sonarqube {
    properties {
        property "sonar.projectKey", "ksch-workflows_backend"
        property "sonar.organization", "ksch-workflows"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "./build/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml"
    }
}
